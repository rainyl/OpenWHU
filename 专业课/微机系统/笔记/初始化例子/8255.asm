; P177 甲乙两台微机并行通信
; -----------------------------------------------------------------
; 微机甲用方式 1 输出，把 PA 端口指定为输出，发送数据
; PC7 和 PC6 引脚由方式 1 规定作为联络线 
; -----------------------------------------------------------------
; 微机乙以方式 0 作为输入，把 PA 口用作输入
; 可选择 PC4 和 PC0 作为联络信号线，PC4 输入，PC0 输出
;------------------------------------------------------------------

; 本装置甲、乙两方端口地址设定为：300H ~ 303H，发送、接收数据长度为 1KB
; 发送端(甲)接收端(乙)存放数据的起点为 0030:0000H、0040:0000H

; ----------------------------------------------------------------------------------------
; 微机甲发送程序
; 82C55A 控制端口
; 当端口地址为 16 位时，需要通过 dx 访问
mov dx, 303h
; 设置方式控制字，A 组工作于方式 1，端口 A 为输出，不关心端口 B 
mov al, 10100000b
; 输出方式字
out dx, al
; 0dh = 00001101b，高位为 0 表示进行端口 C 位设置
; 此处表示设置 P6(INTE) = 1
mov al, 0dh
out dx, al

; 发送数据的首地址
mov ax, 030h
; 设置段寄存器
mov es, ax
; 设置数组指针
mov bx, 00h

; 设置发送字节数，这里为 1KB
mov cx, 3ffh
; 设置 dx 指向端口 A 的地址
mov dx, 300h
; 要发送的第一个数据
mov al, es:[bx]
; 送第一个数，P7(OBF) 产生信号
out dx, al
; 指向下一个字节
inc bx
; 字节数减 1
dec cx

L:
; 设置 dx 指向 82C55A 的状态口(端口C)
mov dx, 302h
; 接受输入状态
in al, dx
; 检查PC3(INTR)是否为1
; 当中断允许且 OBF = 1 的条件下, 由接收到的 ACK 信号上升沿产生, 代表对方已经接收到数据
; 08h = 00001000b 
and al, 08h
; 如果没有中断请求则等待
jz L
; 从端口A输出
mov dx, 300h
; 取数据
mov al, es:[bx]
; 输出
out dx, al
inc bx
dec cx
; 未发送完则循环，即 cx = 0?
jnz L

; 返回 dos
mov ax, 4c00h
int 21h



; ------------------------------------------------------------------------------------
; 微机乙接收程序
; 设置 dx 为控制端口地址
mov dx, 303h
; 设置方式控制字，A 组工作于方式 0，端口 A 输入
; PC4 接受 OBF, PC0 传递 ACK
mov al, 10011000b
out dx, al
; 将 PC0 置 1
mov al, 00000001b
out dx, al

; 接受区的首地址
mov ax, 040h
; 设置段基址
mov es, ax
; 设置数组指针
mov bx, 00h
; 设置接受字节数
mov cx, 3ffh

L1:
; 设置 dx 端口 C
mov dx, 302h
; 查看 PC4 是否为 0
in al, dx
and al, 10h
; 如果 PC4 为 0, 代表接收到 OBF 信号, 此时需要准备接收数据
jnz L1
; 设置 dx 为端口 A 地址
mov dx, 300h
; 接受数据
in al, dx
; 将数据存入内存
mov es:[bx], al
mov dx, 303h
; PC0 = 0
mov al, 00000000b
; 设置 PC0 = 0, 产生 ack 信号
out dx, al
nop
nop
; PC0 = 1
mov al, 00000001b
; ack 变高
out dx, al
inc bx
dec cx
jnz L1

mov ax, 4c00h
int 21h